; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "MEAL (Entrega Automatizada de Lanches)"
#define MyAppVersion "2.8.42"
#define MyAppPublisher "Hugo, Maysa, Lorran"
#define MyAppExeName "SistemaDePaes.jar"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{65DFD5F5-A41B-441F-BF37-A0A2D2B5425A}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={pf}\{#MyAppName}
DefaultGroupName={#MyAppName}
OutputDir=C:\Users\Hugo\Documents\NetBeansProjects\SistemaDePaes
OutputBaseFilename=instalador{#MyAppVersion}
SetupIconFile=C:\Users\Hugo\Documents\NetBeansProjects\SistemaDePaes\imagens\ico2.ico
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"
Name: "brazilianportuguese"; MessagesFile: "compiler:Languages\BrazilianPortuguese.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "C:\Users\Hugo\Documents\NetBeansProjects\SistemaDePaes\MEAL JAR\SistemaDePaes.jar"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Hugo\Documents\NetBeansProjects\SistemaDePaes\JRE-7u65.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\Users\Hugo\Documents\NetBeansProjects\SistemaDePaes\MEAL JAR\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename:"{app}\imagens\ico2.ico"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"; IconFilename:"{app}\imagens\ico2.ico"
Name: "{commondesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; Tasks: desktopicon; IconFilename:"{app}\imagens\ico2.ico"

[Run]
Filename: "{app}\JRE-7u65.exe"; StatusMsg: "{cm:installing_jre}"; Check: CheckJRE; Flags: runhidden
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: shellexec postinstall skipifsilent

[Dirs]
Name: {app};Permissions: users-modify


[CustomMessages]
brazilianportuguese.jre_not_found= {#MyAppName}%n Não foi encontrado a máquina virtual do java. %n%nDeseja instalar a máquina virtual agora?
brazilianportuguese.jre_too_old= {#MyAppName} %nVersão do java muito antiga. %n É necessário instalar uma versão mais recente. %n%nDeseja instalar a versão mais recente agora?
brazilianportuguese.installing_jre=Java está sendo instalado... aguarde alguns segundos....

[code]
Function CheckJRE(): Boolean;
var
  CurrentJREVersion: String;
  NecessaryJREVersion: String;
  JRENotFound: Boolean;
  JRETooOld: Boolean;
  InstallJRE: Boolean;
 
begin
 
  NecessaryJREVersion := '1.7';
  JRENotFound := False;
  JRETooOld := False;
 
  // check for JRE in registry  
  if (RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\JavaSoft\Java Runtime Environment', 'CurrentVersion', CurrentJREVersion)) then
  begin
    JRETooOld := (CurrentJREVersion < NecessaryJREVersion);
  end else begin
    JRENotFound := True;
  end;
 
  // ask user
  InstallJRE := False;
 
  if (JRENotFound) then
  begin
 
    if (MsgBox(ExpandConstant('{cm:jre_not_found}'), mbinformation, mb_YesNo) = idYes) then
    begin
      InstallJRE := True;
    end;
  end;
 
  if (JRETooOld) then
  begin
    if (MsgBox(ExpandConstant('{cm:jre_too_old}'), mbinformation, mb_YesNo) = idYes) then
    begin
      InstallJRE := True;
    end;
  end;
 
  Result := InstallJRE;
 
end;

